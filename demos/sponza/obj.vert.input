#version 450

#extension GL_ARB_separate_shader_objects : enable

const int MAX_INSTANCES = 16;
const int MAX_MATERIALS_PER_MODEL = 32;

INCLUDE(../../data/glsl/lighting.glsl)

layout(push_constant) uniform pcb {
   mat4 Projection;
} PCB;

layout(std140, set = 0, binding = 0) uniform ubo_camera {
   mat4 View;
} CD;

struct ObjData {
   mat4 Model;
   uint material_base_idx;
   uint model_idx;
   uint receives_shadows;
};

layout(std140, set = 1, binding = 0) uniform ubo_obj_data {
   ObjData data[MAX_INSTANCES];
} OID;

layout(std140, set = 2, binding = 0) uniform light_ubo
{
   Light sun;
} L;

struct ShadowMapData {
   mat4 light_viewproj;
   uint shadow_map_size;
   uint pfc_kernel_size;
};

layout(std140, set = 2, binding = 1) uniform ubo_shadow_map_data {
   ShadowMapData shadow_map_data;
} SMD;

layout(location = 0) in vec3 in_position;
layout(location = 1) in vec3 in_normal;
layout(location = 2) in vec3 in_tangent;
layout(location = 3) in vec3 in_bitangent;
layout(location = 4) in vec2 in_uv;
layout(location = 5) in uint in_material_idx;

layout(location = 0) out vec2 out_uv;
layout(location = 1) flat out uint out_material_idx;
layout(location = 2) out vec3 out_eye_normal;
layout(location = 3) out vec4 out_eye_pos;
layout(location = 4) out vec3 out_eye_view_dir;
layout(location = 5) out vec4 out_eye_light_pos;
layout(location = 6) out vec4 out_light_space_pos;
layout(location = 7) out vec3 out_eye_tangent;
layout(location = 8) out vec3 out_eye_bitangent;
layout(location = 9) out vec3 out_debug;

void main()
{
   ObjData obj_data = OID.data[gl_InstanceIndex];

   // UV coordinates need y-flipping
   out_uv = vec2(in_uv.x, -in_uv.y);

   // Compute the material index in the gloabl UBO for this vertex
   out_material_idx =
      obj_data.model_idx * MAX_MATERIALS_PER_MODEL +
      obj_data.material_base_idx + in_material_idx;

   // Compute world space positon
   vec4 model_pos = vec4(in_position.x, in_position.y, in_position.z, 1.0);
   mat4 Model = obj_data.Model;
   vec4 world_pos = Model * model_pos;

   // Compute camera/eye space position
   out_eye_pos = CD.View * world_pos;
   gl_Position = PCB.Projection * out_eye_pos;

   // Compute eye space normal, tangent and bitangent
   mat3 Normal = transpose(inverse(mat3(CD.View * Model)));
   out_eye_normal = normalize(Normal * in_normal);
   out_eye_tangent = normalize(Normal * in_tangent);
   out_eye_bitangent = normalize(Normal * in_bitangent);

    // Compute the vector from this vertex to the camera (in camera space)
   out_eye_view_dir = -out_eye_pos.xyz;
   out_eye_light_pos = vec4(vec3(CD.View * L.sun.pos), L.sun.pos.w);

   // Light space position
   out_light_space_pos = SMD.shadow_map_data.light_viewproj * world_pos;

//   out_debug = out_eye_view_dir;
}
