#version 450

#extension GL_ARB_separate_shader_objects : enable

INCLUDE(../glsl/util.glsl)

/**
 * Does a simple blur pass over the input SSAO texture. To prevent the "halo"
 * effect at the edges of geometry caused by blurring together pixels with
 * and without occlusion, we only blur together pixels that have "similar"
 * depth values.
 */
layout(push_constant) uniform pcb {
   float threshold;
   float depth_near;
   float depth_far;
} PCB;

layout (constant_id = 0) const int BLUR_SIZE = 2;

layout(set = 0, binding = 0) uniform sampler2D tex_ssao;
layout(set = 0, binding = 1) uniform sampler2D tex_depth;

layout(location = 0) in vec2 in_uv;

layout(location = 0) out float out_blur_ssao;

void main()
{
   vec2 texel_size = 1.0 / vec2(textureSize(tex_ssao, 0));

   float result = texture(tex_ssao, in_uv).r;
   float depth_ref = texture(tex_depth, in_uv).r;
   float linear_depth_ref =
      compute_eye_z_from_depth(depth_ref, PCB.depth_near, PCB.depth_far);
   int sample_count = 1;

   for (int x = -BLUR_SIZE; x < BLUR_SIZE; ++x)
   {
      for (int y = -BLUR_SIZE; y < BLUR_SIZE; ++y)
      {
         if (x != 0 || y != 0) {
            vec2 tex_offset = in_uv + vec2(x, y) * texel_size;
            tex_offset = min(vec2(1.0), max(vec2(0.0), tex_offset));
            float depth = texture(tex_depth, tex_offset).r;
            float linear_depth =
               compute_eye_z_from_depth(depth, PCB.depth_near, PCB.depth_far);
            if (abs(linear_depth - linear_depth_ref) < PCB.threshold) {
               float value = texture(tex_ssao, tex_offset).r;
               result += value;
               sample_count++;
            }
         }
      }
   }

   out_blur_ssao = result / float(sample_count);
}
